buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
		classpath 'com.android.tools.build:gradle:1.5.0'
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-4584'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	compileSdkVersion 19
	buildToolsVersion "23.0.2"

	defaultConfig {
		applicationId "meh.watchdoge"

		versionCode 1
		versionName "1.0"

		minSdkVersion 19
		targetSdkVersion 19
	}

	lintOptions {
		abortOnError false
	}

	packagingOptions {
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/LICENSE'
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules.pro'
		}
	}
}

repositories {
	jcenter()
}

dependencies {
	compile 'com.android.support:appcompat-v7:19.0.1'
	compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.0-beta-4584'

	compile 'org.jetbrains.anko:anko-sdk19:0.8'
	compile 'org.jetbrains.anko:anko-support-v4:0.8'
	compile 'org.jetbrains.anko:anko-appcompat-v7:0.8'

	compile 'org.msgpack:msgpack-core:0.8.1'
}

task packageExecutable(type: Copy){
	from 'libs/armeabi/backend'
	into 'src/main/res/raw/'
}

task ndkBuild(type: Exec, description: "Task to run ndk-build") {
	commandLine 'ndk-build'
}

task ndkClean(type: Exec, description: "Task to run ndk-build clean"){
	commandLine 'ndk-build', 'clean'

	doLast {
		delete 'src/main/res/raw/'
	}
}

packageExecutable.dependsOn 'ndkBuild'
preBuild.dependsOn 'packageExecutable'
clean.dependsOn 'ndkClean'
